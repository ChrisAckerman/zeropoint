#!/usr/bin/env zsh
autoload -Uz add-zsh-hook
autoload -Uz git_utils && git_utils

prompt_zeropoint_setup() {
    if [[ -o login ]]; then
        _PROMPT_ZEROPOINT_FIRST=true
    fi

    prompt_zeropoint_user_color=${1:-'green'}
    prompt_zeropoint_path_color=${2:-'blue'}
    prompt_zeropoint_git_color=${3:-'magenta'}

	PS1='%{%B%F{$prompt_zeropoint_user_color}%}%n@%M%{%f%b%}:'
    PS1+='%{%B%F{$prompt_zeropoint_path_color}%}%~%{%f%b%}'
    PS1+='${GIT_BRANCH:+:$GIT_BRANCH}\$ '
	PS2='%_> '
	PS3='?# '
	PS4='+'

	prompt_opts=(cr percent subst)

    add-zsh-hook precmd prompt_zeropoint_precmd
}

prompt_zeropoint_precmd() {
    # Print non-zero exit code and elapsed time if necessary.
    if [[ clear != "$LAST_COMMAND" && -z "$_PROMPT_ZEROPOINT_FIRST" ]]; then
        if [[ -n "$LAST_COMMAND" ]]; then
            local code=${LAST_COMMAND_EXIT_CODE:-0}
            local elapsed=${LAST_COMMAND_ELAPSED:-0}

            if [[ "$code" -gt 0 ]]; then
                print -P "%K{52} Exit Code $code %E%k"
            fi

            if [[ "$elapsed" -ge 5 ]]; then
                elapsed=`print -f %02d:%02d:%02d \
                    $((elapsed / 3600)) \
                    $(((elapsed / 60) % 60)) \
                    $((elapsed % 60))`

                print -P "%K{236} Elapsed Time $elapsed %E%k"
            fi
        fi

        echo
    fi

    # Update the git branch/reference so it can be displayed in the prompt.
    GIT_BRANCH=`git_branch || git_reference '#' || true`
    [[ -n "$GIT_BRANCH" ]] && GIT_BRANCH='%{%B%F{$prompt_zeropoint_git_color}%}'"$GIT_BRANCH"'%{%f%b%}'

    unset _PROMPT_ZEROPOINT_FIRST
}

prompt_zeropoint_setup "$@"

return 0
